group 'jaemisseo'

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = JavaVersion.VERSION_1_8

/*************************
 * If Parent Gradle is modeRemoteDependencies,
 *      Then, No need build.
 *************************/
if (project.hasProperty('modeParentRemoteDependencies')){
    boolean modeParentRemoteDependencies = project.getProperty('modeParentRemoteDependencies')
    if (modeParentRemoteDependencies){
        tasks.each{ it.onlyIf{false} }
        clean.onlyIf{false}
        build.onlyIf{false}
    }
}



sourceSets {
    main {
        groovy {
            srcDirs = ['src/main/java']
        }
    }

    test {
        groovy {
            srcDirs = ['src/test/java']
        }
    }
}



configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    jcenter()
    maven { url "http://central.maven.org/maven2/" }
    maven { url "https://jitpack.io" }
    maven { url 'http://repo.spring.io/milestone' }
    maven { url "http://maven.springframework.org/release" }
}

String coreBasePath = project.hasProperty("coreBasePath") ? project.getProperty("coreBasePath") : ""

dependencies {

    compile ( (rootProject?.findProject("${coreBasePath}:common-util")) ? project(":${coreBasePath}:common-util") : 'com.github.avaj-java:common-util:0.4.1' )

    //SLF4j
    compile 'org.slf4j:slf4j-api:1.7.7'

    //TEST
    testCompile 'junit:junit:4.11'
    testCompile 'ch.qos.logback:logback-classic:1.2.3'
    testCompile 'ch.qos.logback:logback-core:1.2.3'

    //The module that added this module as a dependency decides.
//    testCompile 'com.oracle:ojdbc6:11.2.0.3'
//    testCompile 'mysql:mysql-connector-java:5.1.46'

    /** Local File System으로부터 참조(*.jar) **/
    compile fileTree(dir: 'external_lib', include: ['*.jar'])
    compile fileTree(dir: 'external_lib_dev', include: ['*.jar'])
}

//- (JAR빌드시) 개발용 개발시에만 필요한(배포시에는 필요없는) 파일 제외하기
jar.rootSpec.exclude( fileTree(dir: 'external_lib_dev', include: ['*.jar']).collect{ "**/${it.name}" } )
